plugins {
	id 'java'
	id 'eclipse'
}

version = '0.0.1'

sourceCompatibility = 1.8

repositories {
    jcenter()
}

dependencies {
	compile files('lib/SweetHome3D-5.5.2.jar')
	compile 'java3d:j3d-core:1.3.1'
}

jar {
	extension = 'sh3p'
}

eclipse.classpath.file {
	whenMerged { classpath -> 
		classpath.entries.findAll { entry -> entry.path.endsWith 'SweetHome3D-5.5.2.jar' }*.sourcePath = fileReference('lib/SweetHome3D-5.5.2-src.zip')
	}
}

task publish(type: Copy) {
	from jar
	
	if (System.properties['os.name'].toLowerCase().contains('windows')) {
		into System.getenv('APPDATA') + '/eTeks/Sweet Home 3D/plugins'
	} else {
		into System.getProperty("user.home") + '/.eteks/sweethome3d/plugins'
	}
}

task wrapper(type: Wrapper) {
    gradleVersion = '4.2'
}
